subprojects { subproject ->
    afterEvaluate {
        if (subproject.plugins.hasPlugin('com.android.library')) {
            // If the project is not enabled, return
            if (!subproject.ext.PUBLISH_ENABLED) {
                return
            }

            subproject.ext.PUBLISH_GROUP_ID = 'io.github.4327b9fb.playerbase'
            //subproject.ext.PUBLISH_VERSION = subproject.ext.playerbaseVersion

            def PUBLISH_GROUP_ID = subproject.ext.PUBLISH_GROUP_ID
            def PUBLISH_ARTIFACT_ID = subproject.ext.PUBLISH_ARTIFACT_ID
            def PUBLISH_VERSION = subproject.ext.PUBLISH_VERSION
            def PUBLISH_NAME = subproject.ext.PUBLISH_NAME
            def PUBLISH_DESCRIPTION = subproject.ext.PUBLISH_DESCRIPTION

            /*subproject.android {
                publishing {
                    singleVariant("release") {
                        withSourcesJar()
                        withJavadocJar()
                    }
                }
            }*/

            subproject.apply plugin: 'com.vanniktech.maven.publish'

            mavenPublishing {
                // Set the coordinates manually, but you can also use the Gradle properties
                coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, PUBLISH_VERSION)

                // Publish to Maven Central, but automatic release is set as false
                publishToMavenCentral(false)

                // Set the signing credentials
                signAllPublications()

                // Self-explanatory metadata for the most part
                pom {
                    name = PUBLISH_NAME
                    description = PUBLISH_DESCRIPTION
                    inceptionYear = '2017'
                    // If your project has a dedicated site, use its URL here
                    url = 'https://github.com/jiajunhui/PlayerBase'
                    // Set your license
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'junhuijia'
                            name = 'junhui jia'
                            email = 'jiajunhuiplay@gmail.com'
                        }
                    }

                    // Version control info, if you're using GitHub, follow the format as seen here
                    scm {
                        connection = 'scm:git:github.com/jiajunhui/PlayerBase.git'
                        developerConnection = 'scm:git:ssh://github.com/jiajunhui/PlayerBase.git'
                        url = 'https://github.com/jiajunhui/PlayerBase/tree/master'
                    }
                }
            }
        }
    }
}